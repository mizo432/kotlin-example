plugins {
    id 'org.springframework.boot' version "3.1.6"
    id 'io.spring.dependency-management' version "1.1.4"
    id 'net.researchgate.release' version '3.0.2'
    id 'java'
    id 'idea'
    id "com.github.ben-manes.versions" version '0.48.0'
    id 'se.patrikerdes.use-latest-versions' version '0.2.18'
    id "com.github.spotbugs" version "5.1.3"
    id 'jacoco'
    id("com.gorylenko.gradle-git-properties") version "2.4.1"

}

apply plugin: 'idea'
apply plugin: 'com.github.spotbugs'
apply plugin: 'jacoco'
apply plugin: 'com.github.ben-manes.versions'

group = 'undefined'
version = '0.0.1-SNAPSHOT'

springBoot {
    buildInfo()
}
bootJar {
    enabled = true
}
jar {
    enabled = false
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
idea {
    module {
        outputDir file('build/classes/main')
        testOutputDir file('build/classes/test')
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'de.codecentric:spring-boot-admin-server:3.1.8'
    implementation group: 'de.codecentric', name: 'spring-boot-admin-server-ui', version: "3.1.8"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "de.codecentric:spring-boot-admin-starter-client:3.1.8"
    implementation group: 'org.springframework.boot', name: 'spring-boot-actuator', version: "3.1.4"
    implementation("org.springframework.cloud:spring-cloud-starter-netflix-eureka-client")

    compileOnly group: 'org.jetbrains', name: 'annotations', version: "24.0.1"


}
dependencyManagement {
    imports {
        mavenBom("org.springframework.cloud:spring-cloud-dependencies:2022.0.4")
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

}

test {
    // 失敗しても後続の処理を継続させる
    ignoreFailures = true
    useJUnitPlatform {
        includeTags 'SMALL'
        excludeTags('MEDIUM', 'LARGE')
    }
    reports {
        junitXml.required = true
    }
}

tasks.register('testMedium', Test) {
    ignoreFailures = false
    maxHeapSize("3g")
    useJUnitPlatform {
        includeTags('MEDIUM')
        excludeTags('SMALL', 'LARGE')
    }
    reports {
        junitXml.required = true
    }
}
tasks.register('testLarge', Test) {
    ignoreFailures = false
    useJUnitPlatform {
        includeTags('LARGE')
        excludeTags('SMALL', 'MEDIUM')
    }
    reports {
        junitXml.required = true
    }
}
tasks.register('testReport', TestReport) {
    reportOn subprojects.tasks.test.binResultsDir
    fileTree(dir: it, exclude: ['**/*Test.class', '**/**test/*.class'])
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
    afterEvaluate {
        classDirectories.from(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: ['**/*Test.class', '**/**test/*.class'])
                }))
    }
}
def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA', 'JRE11'].any { it -> version.toUpperCase().contains(it) }
    def unstableKeyword = ['PREVIEW', 'ALPHA', 'M', 'BETA', 'RC', 'RC-1'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return ((!stableKeyword) && (!(version ==~ regex))) || (unstableKeyword)
} as Object

dependencyUpdates {
    // checkForGradleUpdate = true
    // reject all non stable versions
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }

}



